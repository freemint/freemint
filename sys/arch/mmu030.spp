; This file belongs to FreeMiNT.  It's not in the original MiNT 1.12
; distribution.  See the file Changes.MH for a detailed log of changes.

;
; set_mmu(crp,tc): called once, the first time a page table is built, to 
; switch away from the MMU setup that came from the ROM and into the setup 
; that we just built.  The CRP is actually on the stack, and it's 8 bytes.
; The TC is four bytes at sp@(0xc).  "Nulls" is here because we need to
; shove zeros into a few places.
;
%ifndef MMU040
	DATA
nulltc:	dc.l	0

	TEXT

	XDEF	_set_mmu
	XREF	_offset_tt_ram

_set_mmu:

	tst.l	_offset_tt_ram
	beq	normal_set
	movem.l	d0-d2/a0-a2,-(sp)
        move.l	#end_set_mmu_called_st_ram-set_mmu_called_st_ram,d1
        clr.w	-(sp)			;ST-ram
        move.l	d1,-(sp)
        move.w	#68,-(sp)		;Mxalloc
        trap	#1
        addq.l	#8,sp
	tst.l	d0
        beq	normal_set		;no ST-ram
	move.l	d0,a2
	move.l	28(sp),crp2		;save crp & tc in ST-ram
	move.l	32(sp),crp2+4
	move.l	36(sp),tc2
	lea	set_mmu_called_st_ram,a0
	move.l	a2,a1
        lsr.l	#1,d1
        subq.l	#1,d1
copy_st_ram:	move.w	(a0)+,(a1)+
	dbf	d1,copy_st_ram
        move.w	sr,-(sp)
        or.w	#$700,sr		;interrupts locked
        movec	cacr,d0
        or.w	#$808,d0		;clear 030 caches
        movec	 d0,cacr
	jsr	(a2)			;set_mmu called in ST-ram
	move.w	(sp)+,sr   		;interrupts unlocked
	pea	(a2)
	move	#73,-(sp)		;Mfree
	trap	#1
	addq.l	#6,sp
	movem.l	(sp)+,d0-d2/a0-a2
	rts
normal_set:
	pmove	(nulltc).l,tc		; turn off mmu
	dc.l	$f0390800,nulltc	; pmove nulltc,tt0
	dc.l	$f0390c00,nulltc	; pmove nulltc,tt1
	pmove	4(sp),crp		; caution: crp is 8 bytes
	pflusha
	pmove	$c(sp),tc
	rts
	
set_mmu_called_st_ram:

	lea	nulltc2(pc),a0
	pmove	(a0),tc			; turn off mmu
        pmove	(a0),tt0
        pmove	(a0),tt1
	lea	crp2(pc),a0
	pmove	(a0),crp		; caution: crp is 8 bytes
	pflusha
	lea	tc2(pc),a0
	pmove	(a0),tc
	rts	
nulltc2:	dc.l	0
crp2:	dc.l	0,0
tc2:	dc.l	0
end_set_mmu_called_st_ram:

;
; save_mmu, restr_mmu: save and restore the MMU setup that came from ROM
;
	DATA
oldcrp:	dc.l	0
	dc.l	0
oldtc:	dc.l	0
oldtt0:	dc.l	0
oldtt1:	dc.l	0

	TEXT

	XDEF	_save_mmu

_save_mmu:
	pmove	tc,oldcrp+8			;,oldtc
	dc.l	$f0390a00,oldtt0	; pmove	tt0,oldtt0
	dc.l	$f0390e00,oldtt1	; pmove	tt1,oldtt1
	pmove	crp,oldcrp
	rts


	XDEF	_restr_mmu

_restr_mmu:
	pmove	(nulltc).l,tc
	dc.l	$f0390800,oldtt0	; pmove	oldtt0,tt0
	dc.l	$f0390c00,oldtt1	; pmove	oldtt1,tt1
	pmove	oldcrp,crp
	pmove	oldtc,tc
	rts


	XDEF	_flush_mmu

_flush_mmu:
	pflusha
	rts

%else
	; it seems GNU ld doesn't like completely empty object files
	TEXT
	XDEF	mmu030dummy

mmu030dummy:
	nop

%endif
